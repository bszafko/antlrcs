/*
 [The "BSD licence"]
 Copyright (c) 2003-2009 Terence Parr
 All rights reserved.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:
 1. Redistributions of source code must retain the above copyright
    notice, this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright
    notice, this list of conditions and the following disclaimer in the
    documentation and/or other materials provided with the distribution.
 3. The name of the author may not be used to endorse or promote products
    derived from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

grammar Group;

options {
	language=CSharp3;
}

@lexer::namespace{StringTemplate.Compiler}
@parser::namespace{StringTemplate.Compiler}

@header
{
using Console = System.Console;
}

public
group[TemplateGroup group, TemplateName prefix]
@init {
GroupLexer lexer = (GroupLexer)input.TokenSource;
this._group = lexer._group = $group;
}
	:	def[prefix]+
    ;

/** Match template and dictionary defs outside of (...)+ loop in group.
 *  The key is catching while still in the loop; must keep prediction of
 *  elements separate from "stay in loop" prediction.
 */
def[TemplateName prefix]
	:	templateDef[prefix]
	|	dictDef
	;
catch[RecognitionException re]
{
	sync("garbled template definition starting at '" + input.LT(1).Text + "'");
}

public
templateDef[TemplateName prefix]
@init {
    string template=null;
    TemplateName fullName = null;
    int n=0; // num char to strip from left, right of template def
}
	:	(	'@' enclosing=ID '.' region=ID '(' ')'
			{fullName = TemplateGroup.GetMangledRegionName(new TemplateName($enclosing.text), $region.text);}
		|	name=ID '(' formalArgs? ')' {fullName = new TemplateName($name.text);}
		)
		'::='
		{IToken templateToken = input.LT(1);}
		(	STRING     {template=$STRING.text; n=1;}
		|	BIGSTRING  {template=$BIGSTRING.text; n=2;}
		|	{
			template = "";
			string msg = "missing template at '" + input.LT(1).Text + "'";
			NoViableAltException e = new NoViableAltException("", 0, 0, input);
			ErrorManager.SyntaxError(ErrorType.SyntaxError, e, msg, SourceName);
			}
		)
		{
		template = Misc.Strip(template, n);
		if ( templateToken.Type==BIGSTRING ) {
			template = Misc.TrimOneStartingWS(template);
		}
	    try {
			if ($enclosing != null)
				this._group.DefineRegion(prefix, new TemplateName($enclosing.text), $region.text, template);
			else
				this._group.DefineTemplate(prefix, new TemplateName($name.text), $formalArgs.args, template);
		}
		catch (TemplateException e)
		{
			RecognitionException re = (RecognitionException)e.InnerException;
			re.Token.CharPositionInLine = re.CharPositionInLine + templateToken.CharPositionInLine + n;
			re.Token.Line = re.Line + templateToken.Line - 1;
			ErrorManager.SyntaxError(ErrorType.SyntaxError, re, e.Message);
		}
	    }
	|   alias=ID '::=' target=ID	    
	;

formalArgs returns[IDictionary<string,FormalArgument> args]
@init {$args = new Dictionary<string,FormalArgument>();}
    :	formalArg[$args] ( ',' formalArg[$args] )*
	;

formalArg[IDictionary<string,FormalArgument> args]
	:	ID
		(	'=' a=STRING
		|	'=' a=ANONYMOUS_TEMPLATE
		)?
		{$args[$ID.text] = new FormalArgument($ID.text, $a);}
    ;

/*
suffix returns [int cardinality=FormalArgument.REQUIRED]
    :   OPTIONAL 
    |   STAR     
    |   PLUS     
	|
    ;
    */

dictDef
	:	ID '::=' dict
        {
        if ( this._group.dictionaries.ContainsKey($ID.text) ) {
            ErrorManager.CompileTimeError(ErrorType.MapRedefinition, "redefinition of map: "+$ID.text);
        }
        else if ( this._group.templates.ContainsKey(new TemplateName($ID.text)) ) {
            ErrorManager.CompileTimeError(ErrorType.TemplateRedefinitionAsMap, "redefinition of template as map: "+$ID.text);
        }
        else {
            this._group.DefineDictionary($ID.text, $dict.mapping);
        }
        }
	;

dict returns [IDictionary<string, object> mapping]
@init {mapping=new Dictionary<string, object>();}
	:   '[' dictPairs[mapping] ']'
	;
	
dictPairs[IDictionary<string, object> mapping]
    :	keyValuePair[mapping]
    	(',' keyValuePair[mapping])* (',' defaultValuePair[mapping])?
    |	defaultValuePair[mapping] 
    ;	
	
defaultValuePair[IDictionary<string,object> mapping]
	:	'default' ':' keyValue {mapping[TemplateGroup.DEFAULT_KEY] = $keyValue.value;}
	;

keyValuePair[IDictionary<string,object> mapping]
	:	STRING ':' keyValue {mapping[Misc.ReplaceEscapes(Misc.Strip($STRING.text, 1))] = $keyValue.value;}
	;

keyValue returns [object value]
	:	BIGSTRING			{$value = new Template(Misc.Strip($BIGSTRING.text,2));}
	|	ANONYMOUS_TEMPLATE	{$value = new Template(Misc.Strip($ANONYMOUS_TEMPLATE.text,1));}
	|	STRING				{$value = Misc.ReplaceEscapes(Misc.Strip($STRING.text, 1));}
	|	{input.LT(1).Text.Equals("key")}?=> ID
							{$value = TemplateGroup.DICT_KEY;}
	|						{$value = null;}
	;

sync[string msg]
@init
{
	// Consume any garbled tokens that come before the next statement
	// or the end of the block.
	int mark = input.Index;
	IToken t = input.LT(1);
	Recover(input, null);
	if (input.Index != mark)
	{
		// we consumed bad tokens
		NoViableAltException e = new NoViableAltException("", 0, 0, input);
		e.Token = t;
		ErrorManager.SyntaxError(ErrorType.SyntaxError, e, msg, SourceName);
		Recover(input, null);
	}
}
	:	// Deliberately match nothing, causing this rule always to be 
		// entered.
	;

ID	:	('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'0'..'9'|'-'|'_')*
	;

STRING
	:	'"' ( '\\' '"' | '\\' ~'"' | ~('\\'|'"') )* '"'
	;

BIGSTRING
	:	'<<'
		(	options {greedy=false;}
		:	'\\' '>'  // \> escape
		|	'\\' ~'>'
		|	~'\\'
		)*
        '>>'
        {
        $text = $text.Replace(@"\>", ">");
        }
	;

ANONYMOUS_TEMPLATE
    :	'{'
    	{
		TemplateLexer lexer =
			new TemplateLexer(input, this._group.delimiterStartChar, this._group.delimiterStopChar);
		lexer.subtemplateDepth = 1;
		CommonTokenStream tokens = new CommonTokenStream(lexer);
        TemplateParser parser = new TemplateParser(tokens, (ICodeGenerator)null, null);
		parser.template();
		}
    	// don't match '}' here; subparser matches it to terminate
    ;

COMMENT
    :   '/*' ( options {greedy=false;} : . )* '*/' {Skip();}
    ;

LINE_COMMENT
    :	'//' ~('\n'|'\r')* '\r'? '\n' {Skip();}
    ;

WS  :	(' '|'\r'|'\t'|'\n') {Skip();} ;
